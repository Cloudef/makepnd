# Maintainer: Jari Vetoniemi <mailRoxas@gmail.com>

pkgname=libarchive
pkgver=3.1.2
pkgrel=4
pkgdesc="library that can create and read several streaming archive formats"
url="http://libarchive.org/"
license=('BSD')
depends=('libacl1' 'libattr1' 'bzip2' 'libexpat1' 'liblzo1' 'openssl' 'libz1' 'xz')
makedepends=('libacl-dev' 'libattr-dev' 'bzip2-dev' 'libexpat-dev' 'liblzo-dev' 'openssl-dev' 'libz-dev')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz"
        '0001-mtree-fix-line-filename-length-calculation.patch'
        https://github.com/libarchive/libarchive/commit/22531545514043e04633e1c015c7540b9de9dbe4.patch
        'libarchive-3.1.2-acl.patch')

build() {
  cd "$pkgname-$pkgver"

  # https://code.google.com/p/libarchive/issues/detail?id=301
  # upstream commit e65bf287f0133426b26611fe3e80b51267987106
  patch -Np1 -i "$srcdir/0001-mtree-fix-line-filename-length-calculation.patch"

  # https://code.google.com/p/libarchive/issues/detail?id=329
  patch -Np1 -i "$srcdir/libarchive-3.1.2-acl.patch"

  # CVE-2013-0211
  patch -Np1 -i "$srcdir/22531545514043e04633e1c015c7540b9de9dbe4.patch"

  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}

md5sums=('efad5a503f66329bb9d2f4308b5de98a'
         'fda89c145bbcd793a96b06b463ef6a72'
         '6a63086b1a2e540d74ca6dc240dbd021'
         'a5c995661c62429ceff2c23ea322393b')

# vim: set ts=8 sw=2 tw=0 ft=sh syn=sh :
